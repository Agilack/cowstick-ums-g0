define reg_rd
	if $argc != 1
		printf "Usage: reg_rd <address>\n"
	end
	if $argc == 1
		printf "%.8lx\n", *(unsigned long *)$arg0
	end
end

define dump
	if $argc != 1
		printf "Usage: dump <address>\n"
	end
	if $argc == 1
		set $i = 0
		while $i < 16
			printf "%.2x ", *(unsigned char *)($arg0 + $i)
			set $i = $i + 1
		end
		printf "\n"
	end
end

define irq_off
	# Save current enable interrupts
	set $nvic_isr = *(unsigned long *)0xE000E100
	# Disable all interrupts into NVIC
	set *(unsigned long *)0xE000E180 = 0xFFFFFFFF
end

define irq_on
	# Restore interrupts saved into irq_off
	set *(unsigned long *)0xE000E100 = $nvic_isr
end

define usb
	if $argc == 0
		echo == USB status ==\n
		printf "ADDR=%.8lx  ", *(unsigned long *)0x40005C4C
		printf "FNR=%.8lx\n",  *(unsigned long *)0x40005C48
		printf "ISPR=%.8lx\n", *(unsigned long *)0x40005C44
		printf "CHEP0R=%.8lx\n", *(unsigned long *)0x40005C00
		printf "  EP0_TX_DESC=%.8lx\n", *(unsigned long *)0x40009800
		printf "  EP0_RX_DESC=%.8lx\n", *(unsigned long *)0x40009804
	end
	if $argc == 1
		printf "%.8lx\n", *(unsigned long *)$arg0
	end
end

target remote localhost:3333
